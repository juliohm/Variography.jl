# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

# models that can be fitted currently
fittable() = filter(isstationary, setdiff(subtypes(Variogram), (NuggetEffect, NestedVariogram)))

"""
    VariogramFitAlgo

An algorithm for fitting theoretical variograms.
"""
abstract type VariogramFitAlgo end

"""
    WeightedLeastSquares()
    WeightedLeastSquares(w)

Fit theoretical variogram using weighted least squares with weighting
function `w` (e.g. h -> 1/h). If no weighting function is provided,
bin counts of empirical variogram are normalized and used as weights.
"""
struct WeightedLeastSquares <: VariogramFitAlgo
  weightfun::Union{Function,Nothing}
end

WeightedLeastSquares() = WeightedLeastSquares(nothing)

"""
    fit(V, g, algo=WeightedLeastSquares(); range=nothing, sill=nothing, nugget=nothing)

Fit theoretical variogram type `V` to empirical variogram `g`
using algorithm `algo`.

Optionally fix `range`, `sill` or `nugget` by passing them as keyword arguments.

## Examples

```julia
julia> fit(SphericalVariogram, g)
julia> fit(ExponentialVariogram, g)
julia> fit(ExponentialVariogram, g, sill=0.5)
julia> fit(GaussianVariogram, g, WeightedLeastSquares())
```
"""
fit(V::Type{<:Variogram}, g::EmpiricalVariogram, algo::VariogramFitAlgo=WeightedLeastSquares(); kwargs...) =
  fit_impl(V, g, algo; kwargs...) |> first

"""
    fit(Vs, g, algo=WeightedLeastSquares(); kwargs...)

Fit theoretical variogram types `Vs` to empirical variogram `g`
using algorithm `algo` and return the one with minimum error.

## Examples

```julia
julia> fit([SphericalVariogram, ExponentialVariogram], g)
```
"""
function fit(Vs, g::EmpiricalVariogram, algo::VariogramFitAlgo=WeightedLeastSquares(); kwargs...)
  # fit each variogram type
  res = [fit_impl(V, g, algo; kwargs...) for V in Vs]
  Œ≥s, œµs = first.(res), last.(res)

  # return best candidate
  Œ≥s[argmin(œµs)]
end

"""
    fit(Variogram, g, algo=WeightedLeastSquares(); kwargs...)

Fit all "fittable" subtypes of `Variogram` to empirical variogram `g`
using algorithm `algo` and return the one with minimum error.

## Examples

```julia
julia> fit(Variogram, g)
julia> fit(Variogram, g, WeightedLeastSquares())
```

See also `Variography.fittable()`.
"""
fit(::Type{Variogram}, g::EmpiricalVariogram, algo::VariogramFitAlgo=WeightedLeastSquares(); kwargs...) =
  fit(fittable(), g, algo; kwargs...)

"""
    fit(V, g, weightfun; kwargs...)
    fit(Variogram, g, weightfun; kwargs...)

Convenience method that forwards the weighting function `weightfun`
to the `WeightedLeastSquares` algorithm.

## Examples

```julia
fit(SphericalVariogram, g, h -> exp(-h))
fit(Variogram, g, h -> exp(-h/100))
```
"""
fit(V, g::EmpiricalVariogram, weightfun::Function; kwargs...) = fit(V, g, WeightedLeastSquares(weightfun); kwargs...)

# ---------------
# IMPLEMENTATION
# ---------------

function fit_impl(
  V::Type{<:Variogram},
  g::EmpiricalVariogram,
  algo::WeightedLeastSquares;
  range=nothing,
  sill=nothing,
  nugget=nothing
)
  # values of empirical variogram
  x, y, n = values(g)

  # custom ball of given radius
  ball(r) = MetricBall(r, distance(g))

  # discard invalid bins
  x = x[n .> 0]
  y = y[n .> 0]
  n = n[n .> 0]

  # strip units if necessary
  ùìä = unit(first(y))
  y = ustrip.(y)

  # auxiliary variables
  xmax, ymax = maximum(x), maximum(y)

  # evaluate weights
  f = algo.weightfun
  w = isnothing(f) ? n / sum(n) : map(f, x)

  # objective function
  function J(Œ∏)
    Œ≥ = V(ball(Œ∏[1]), sill=Œ∏[2], nugget=Œ∏[3])
    sum(w[i] * (Œ≥(x[i]) - y[i])^2 for i in eachindex(x))
  end

  # linear constraint (sill ‚â• nugget)
  L(Œ∏) = Œ∏[2] ‚â• Œ∏[3] ? 0.0 : Œ∏[3] - Œ∏[2]

  # penalty for linear constraint (J + ŒªL)
  Œª = sum(y·µ¢ -> y·µ¢^2, y)

  # initial guess
  r‚Çí = isnothing(range) ? xmax / 3 : range
  s‚Çí = isnothing(sill) ? 0.95 * ymax : sill
  n‚Çí = isnothing(nugget) ? 1e-6 : nugget
  Œ∏‚Çí = [r‚Çí, s‚Çí, n‚Çí]

  # box constraints
  Œ¥ = 1e-8
  r‚Çó, r·µ§ = isnothing(range) ? (0.0, xmax) : (range - Œ¥, range + Œ¥)
  s‚Çó, s·µ§ = isnothing(sill) ? (0.0, ymax) : (sill - Œ¥, sill + Œ¥)
  n‚Çó, n·µ§ = isnothing(nugget) ? (0.0, ymax) : (nugget - Œ¥, nugget + Œ¥)
  l = [r‚Çó, s‚Çó, n‚Çó]
  u = [r·µ§, s·µ§, n·µ§]

  # solve optimization problem
  sol = Optim.optimize(Œ∏ -> J(Œ∏) + Œª * L(Œ∏), l, u, Œ∏‚Çí)
  œµ = Optim.minimum(sol)
  Œ∏ = Optim.minimizer(sol)

  # optimal variogram (with units)
  Œ≥ = V(ball(Œ∏[1]), sill=Œ∏[2] * ùìä, nugget=Œ∏[3] * ùìä)

  Œ≥, œµ
end
