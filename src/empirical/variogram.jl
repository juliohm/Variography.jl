# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    EmpiricalVariogram(data, varâ‚, varâ‚‚=varâ‚; [parameters])

Computes the empirical (a.k.a. experimental) omnidirectional
(cross-)variogram for variables `varâ‚` and `varâ‚‚` stored in
geospatial `data`.

## Parameters

  * nlags     - number of lags (default to `20`)
  * maxlag    - maximum lag (default to 1/10 of maximum lag of data)
  * distance  - custom distance function (default to `Euclidean` distance)
  * estimator - variogram estimator (default to `:matheron` estimator)
  * algorithm - accumulation algorithm (default to `:ball`)

Available estimators:

  * `:matheron` - simple estimator based on squared differences

Available algorithms:

  * `:full` - loop over all pairs of points in the data
  * `:ball` - loop over all points inside maximum lag ball

All implemented algorithms produce the exact same result.
The `:ball` algorithm is considerably faster when the
maximum lag is much smaller than the bounding box of
the data.

See also: [`DirectionalVariogram`](@ref), [`PlanarVariogram`](@ref),
[`EmpiricalVarioplane`](@ref).

## References

* ChilÃ¨s, JP and Delfiner, P. 2012. [Geostatistics: Modeling Spatial Uncertainty]
  (https://onlinelibrary.wiley.com/doi/book/10.1002/9781118136188)
* Webster, R and Oliver, MA. 2007. [Geostatistics for Environmental Scientists]
  (https://onlinelibrary.wiley.com/doi/book/10.1002/9780470517277)
* Hoffimann, J and Zadrozny, B. 2019. [Efficient variography with partition variograms]
  (https://www.sciencedirect.com/science/article/pii/S0098300419302936)
"""
struct EmpiricalVariogram{V,D,E}
  abscissa::Vector{Float64}
  ordinate::Vector{V}
  counts::Vector{Int}
  distance::D
  estimator::E
end

function EmpiricalVariogram(
  data::Data,
  varâ‚,
  varâ‚‚=varâ‚;
  nlags=20,
  maxlag=0.1diagonal(boundingbox(data)),
  distance=Euclidean(),
  estimator=:matheron,
  algorithm=:ball
)

  # retrieve table and domain
  ğ’¯ = values(data)
  ğ’Ÿ = domain(data)

  # retrieve number of elements
  nelem = nelements(ğ’Ÿ)

  # sanity checks
  @assert nelem > 1 "variogram requires at least 2 elements"
  @assert nlags > 0 "number of lags must be positive"
  @assert maxlag > 0 "maximum lag distance must be positive"
  @assert estimator âˆˆ (:matheron,) "invalid variogram estimator"
  @assert algorithm âˆˆ (:full, :ball) "invalid accumulation algorithm"

  estim = if estimator == :matheron
    MatheronEstimator()
  else
    throw(AssertionError("invalid branch reached"))
  end

  # ball search with NearestNeighbors.jl requires AbstractFloat and MinkowskiMetric
  # https://github.com/KristofferC/NearestNeighbors.jl/issues/13
  isfloat = coordtype(ğ’Ÿ) <: AbstractFloat
  isminkowski = distance isa MinkowskiMetric

  # warn users requesting :ball option with invalid parameters
  (algorithm == :ball && !isfloat) && @warn ":ball algorithm requires floating point coordinates, falling back to :full"
  (algorithm == :ball && !isminkowski) && @warn ":ball algorithm requires Minkowski metric, falling back to :full"

  # choose accumulation algorithm
  algo = if algorithm == :ball && isfloat && isminkowski
    BallSearchAccum(nlags, maxlag, distance)
  else
    FullSearchAccum(nlags, maxlag, distance)
  end

  # empirical variograms are defined on point sets
  ğ’« = PointSet([centroid(ğ’Ÿ, i) for i in 1:nelem])
  ğ’® = georef(ğ’¯, ğ’«)

  # accumulate data with chosen algorithm
  xsums, ysums, counts = accumulate(ğ’®, varâ‚, varâ‚‚, estim, algo)

  # bin (or lag) size
  Î´h = maxlag / nlags
  lags = range(Î´h / 2, stop=maxlag - Î´h / 2, length=nlags)

  # variogram abscissa
  abscissa = @. xsums / counts
  abscissa[counts .== 0] .= lags[counts .== 0]

  # variogram ordinate
  ordinate = @. (ysums / counts) / 2
  ordinate[counts .== 0] .= zero(eltype(ordinate))

  EmpiricalVariogram(abscissa, ordinate, counts, distance, estim)
end

"""
    values(Î³)

Returns the abscissa, the ordinate, and the bin counts
of the empirical variogram `Î³`.
"""
Base.values(Î³::EmpiricalVariogram) = Î³.abscissa, Î³.ordinate, Î³.counts

"""
    distance(Î³)

Return the distance used to compute the empirical variogram `Î³`.
"""
distance(Î³::EmpiricalVariogram) = Î³.distance

"""
    estimator(Î³)

Return the estimator used to compute the empirical variogram `Î³`.
"""
estimator(Î³::EmpiricalVariogram) = Î³.estimator

"""
    merge(Î³Î±, Î³Î²)

Merge the empirical variogram `Î³Î±` with the empirical variogram `Î³Î²`
assuming that both variograms have the same number of lags.
"""
function merge(Î³Î±::EmpiricalVariogram{V,D,E}, Î³Î²::EmpiricalVariogram{V,D,E}) where {V,D,E}
  xÎ± = Î³Î±.abscissa
  xÎ² = Î³Î².abscissa
  yÎ± = Î³Î±.ordinate
  yÎ² = Î³Î².ordinate
  nÎ± = Î³Î±.counts
  nÎ² = Î³Î².counts

  d = Î³Î±.distance
  e = Î³Î±.estimator

  n = nÎ± + nÎ²
  x = @. (xÎ± * nÎ± + xÎ² * nÎ²) / n
  y = @. (yÎ± * nÎ± + yÎ² * nÎ²) / n

  # adjust empty bins
  x[n .== 0] .= xÎ±[n .== 0]
  y[n .== 0] .= 0

  EmpiricalVariogram(x, y, n, d, e)
end

# -----------
# IO METHODS
# -----------

function Base.show(io::IO, ::EmpiricalVariogram)
  print(io, "EmpiricalVariogram")
end

function Base.show(io::IO, ::MIME"text/plain", Î³::EmpiricalVariogram)
  println(io, Î³)
  println(io, "  abscissa: ", extrema(Î³.abscissa))
  println(io, "  ordinate: ", extrema(Î³.ordinate))
  print(io, "  NÂ° pairs: ", sum(Î³.counts))
end
