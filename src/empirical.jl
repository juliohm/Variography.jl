# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

# default maximum lag to be used in various methods
default_maxlag(data) = 0.1diagonal(boundingbox(data))

"""
    EmpiricalVariogram(data, var‚ÇÅ, var‚ÇÇ=var‚ÇÅ; [parameters])

Computes the empirical (a.k.a. experimental) omnidirectional
(cross-)variogram for variables `var‚ÇÅ` and `var‚ÇÇ` stored in
geospatial `data`.

## Parameters

  * nlags     - number of lags (default to `20`)
  * maxlag    - maximum lag (default to 1/10 of maximum lag of data)
  * distance  - custom distance function (default to `Euclidean` distance)
  * estimator - variogram estimator (default to `Matheron` estimator)
  * algo      - accumulation algorithm (default to `:ball`)

Available algorithms:

  * `:full` - loop over all pairs of points in the data
  * `:ball` - loop over all points inside maximum lag ball

All implemented algorithms produce the exact same result.
The `:ball` algorithm is considerably faster when the
maximum lag is much smaller than the bounding box of
the data.

See also: [`DirectionalVariogram`](@ref), [`PlanarVariogram`](@ref),
[`EmpiricalVarioplane`](@ref).

## References

* Chil√®s, JP and Delfiner, P. 2012. [Geostatistics: Modeling Spatial Uncertainty]
  (https://onlinelibrary.wiley.com/doi/book/10.1002/9781118136188)
* Webster, R and Oliver, MA. 2007. [Geostatistics for Environmental Scientists]
  (https://onlinelibrary.wiley.com/doi/book/10.1002/9780470517277)
* Hoffimann, J and Zadrozny, B. 2019. [Efficient variography with partition variograms]
  (https://www.sciencedirect.com/science/article/pii/S0098300419302936)
"""
struct EmpiricalVariogram{V,D,E}
  abscissa::Vector{Float64}
  ordinate::Vector{V}
  counts::Vector{Int}
  distance::D
  estimator::E
end

function EmpiricalVariogram(
  data,
  var‚ÇÅ::Symbol,
  var‚ÇÇ::Symbol=var‚ÇÅ;
  nlags=20,
  maxlag=default_maxlag(data),
  distance=Euclidean(),
  estimator=Matheron(),
  algo=:ball
)

  # retrieve table and domain
  ùíØ = values(data)
  ùíü = domain(data)

  # retrieve the column names of data values
  vars = Tables.columnnames(ùíØ)

  # sanity checks
  @assert nelements(ùíü) > 1 "variogram requires at least 2 elements"
  @assert (var‚ÇÅ, var‚ÇÇ) ‚äÜ vars "invalid variable names"
  @assert algo ‚àà (:full, :ball) "invalid accumulation algorithm"
  @assert nlags > 0 "number of lags must be positive"
  @assert maxlag > 0 "maximum lag distance must be positive"

  # ball search with NearestNeighbors.jl requires AbstractFloat and MinkowskiMetric
  # https://github.com/KristofferC/NearestNeighbors.jl/issues/13
  isfloat = coordtype(ùíü) <: AbstractFloat
  isminkowski = distance isa MinkowskiMetric

  # warn users requesting :ball option with invalid parameters
  (algo == :ball && !isfloat) && @warn ":ball algorithm requires floating point coordinates"
  (algo == :ball && !isminkowski) && @warn ":ball algorithm requires Minkowski metric"

  # empirical variograms are defined on point sets
  ùí´ = PointSet([centroid(ùíü, i) for i in 1:nelements(ùíü)])
  ùíÆ = georef(ùíØ, ùí´)

  # choose accumulation algorithm
  if algo == :ball && isfloat && isminkowski
    xsums, ysums, counts = ball_search_accum(ùíÆ, var‚ÇÅ, var‚ÇÇ, maxlag, nlags, distance, estimator)
  else
    xsums, ysums, counts = full_search_accum(ùíÆ, var‚ÇÅ, var‚ÇÇ, maxlag, nlags, distance, estimator)
  end

  # bin (or lag) size
  Œ¥h = maxlag / nlags
  lags = range(Œ¥h / 2, stop=maxlag - Œ¥h / 2, length=nlags)

  # variogram abscissa
  abscissa = @. xsums / counts
  abscissa[counts .== 0] .= lags[counts .== 0]

  # variogram ordinate
  ordinate = @. (ysums / counts) / 2
  ordinate[counts .== 0] .= zero(eltype(ordinate))

  EmpiricalVariogram(abscissa, ordinate, counts, distance, estimator)
end

"""
    values(Œ≥)

Returns the abscissa, the ordinate, and the bin counts
of the empirical variogram `Œ≥`.
"""
Base.values(Œ≥::EmpiricalVariogram) = Œ≥.abscissa, Œ≥.ordinate, Œ≥.counts

"""
    distance(Œ≥)

Return the distance used to compute the empirical variogram `Œ≥`.
"""
distance(Œ≥::EmpiricalVariogram) = Œ≥.distance

"""
    estimator(Œ≥)

Return the estimator used to compute the empirical variogram `Œ≥`.
"""
estimator(Œ≥::EmpiricalVariogram) = Œ≥.estimator

"""
    merge(Œ≥Œ±, Œ≥Œ≤)

Merge the empirical variogram `Œ≥Œ±` with the empirical variogram `Œ≥Œ≤`
assuming that both variograms have the same number of lags.
"""
function merge(Œ≥Œ±::EmpiricalVariogram{V,D,E}, Œ≥Œ≤::EmpiricalVariogram{V,D,E}) where {V,D,E}
  xŒ± = Œ≥Œ±.abscissa
  xŒ≤ = Œ≥Œ≤.abscissa
  yŒ± = Œ≥Œ±.ordinate
  yŒ≤ = Œ≥Œ≤.ordinate
  nŒ± = Œ≥Œ±.counts
  nŒ≤ = Œ≥Œ≤.counts

  d = Œ≥Œ±.distance
  e = Œ≥Œ±.estimator

  n = nŒ± + nŒ≤
  x = @. (xŒ± * nŒ± + xŒ≤ * nŒ≤) / n
  y = @. (yŒ± * nŒ± + yŒ≤ * nŒ≤) / n

  # adjust empty bins
  x[n .== 0] .= xŒ±[n .== 0]
  y[n .== 0] .= 0

  EmpiricalVariogram(x, y, n, d, e)
end

# -----------
# IO METHODS
# -----------

function Base.show(io::IO, ::EmpiricalVariogram)
  print(io, "EmpiricalVariogram")
end

function Base.show(io::IO, ::MIME"text/plain", Œ≥::EmpiricalVariogram)
  println(io, Œ≥)
  println(io, "  abscissa: ", extrema(Œ≥.abscissa))
  println(io, "  ordinate: ", extrema(Œ≥.ordinate))
  print(io, "  N¬∞ pairs: ", sum(Œ≥.counts))
end

# ---------------------
# VARIOGRAM ESTIMATORS
# ---------------------

"""
    VariogramEstimator

A (robust) estimator of [`EmpiricalVariogram`](@ref).
"""
abstract type VariogramEstimator end

include("estimators/matheron.jl")

# ------------------------
# ACCUMULATION ALGORITHMS
# ------------------------

function full_search_accum(data, var‚ÇÅ, var‚ÇÇ, maxlag, nlags, distance, estimator)
  # retrieve table and point set
  ùíØ = values(data)
  ùí´ = domain(data)

  # collect vectors for variables
  cols = Tables.columns(ùíØ)
  Z‚ÇÅ = Tables.getcolumn(cols, var‚ÇÅ)
  Z‚ÇÇ = Tables.getcolumn(cols, var‚ÇÇ)

  # lag size
  Œ¥h = maxlag / nlags

  # accumulation type
  V = typeof((Z‚ÇÅ[1] - Z‚ÇÇ[1]) ‚ãÖ (Z‚ÇÅ[1] - Z‚ÇÇ[1]))

  # lag sums and counts
  xsums = zeros(nlags)
  ysums = zeros(V, nlags)
  counts = zeros(Int, nlags)

  # loop over all pairs of points
  @inbounds for j in 1:nelements(ùí´)
    p‚±º = ùí´[j]
    z‚ÇÅ‚±º = Z‚ÇÅ[j]
    z‚ÇÇ‚±º = Z‚ÇÇ[j]
    for i in (j + 1):nelements(ùí´)
      p·µ¢ = ùí´[i]
      z‚ÇÅ·µ¢ = Z‚ÇÅ[i]
      z‚ÇÇ·µ¢ = Z‚ÇÇ[i]

      # evaluate geospatial lag
      h = evaluate(distance, coordinates(p·µ¢), coordinates(p‚±º))
      h > maxlag && continue # early exit if out of range

      # evaluate (cross-)variance
      v = (z‚ÇÅ·µ¢ - z‚ÇÅ‚±º) ‚ãÖ (z‚ÇÇ·µ¢ - z‚ÇÇ‚±º)

      # bin (or lag) where to accumulate result
      lag = ceil(Int, h / Œ¥h)
      lag == 0 && @warn "duplicate coordinates found"

      if 0 < lag ‚â§ nlags && !ismissing(v)
        xsums[lag] += h
        ysums[lag] += v
        counts[lag] += 1
      end
    end
  end

  xsums, ysums, counts
end

function ball_search_accum(data, var‚ÇÅ, var‚ÇÇ, maxlag, nlags, distance, estimator)
  # retrieve table and point set
  ùíØ = values(data)
  ùí´ = domain(data)

  # collect vectors for variables
  cols = Tables.columns(ùíØ)
  Z‚ÇÅ = Tables.getcolumn(cols, var‚ÇÅ)
  Z‚ÇÇ = Tables.getcolumn(cols, var‚ÇÇ)

  # lag size
  Œ¥h = maxlag / nlags

  # accumulation type
  V = typeof((Z‚ÇÅ[1] - Z‚ÇÇ[1]) ‚ãÖ (Z‚ÇÅ[1] - Z‚ÇÇ[1]))

  # lag sums and counts
  xsums = zeros(nlags)
  ysums = zeros(V, nlags)
  counts = zeros(Int, nlags)

  # fast ball search
  ball = MetricBall(maxlag, distance)
  searcher = BallSearch(ùí´, ball)

  # loop over points inside ball
  @inbounds for j in 1:nelements(ùí´)
    p‚±º = ùí´[j]
    z‚ÇÅ‚±º = Z‚ÇÅ[j]
    z‚ÇÇ‚±º = Z‚ÇÇ[j]
    for i in search(p‚±º, searcher)
      i ‚â§ j && continue # avoid double counting
      p·µ¢ = ùí´[i]
      z‚ÇÅ·µ¢ = Z‚ÇÅ[i]
      z‚ÇÇ·µ¢ = Z‚ÇÇ[i]

      # evaluate geospatial lag
      h = evaluate(distance, coordinates(p·µ¢), coordinates(p‚±º))

      # evaluate (cross-)variance
      v = (z‚ÇÅ·µ¢ - z‚ÇÅ‚±º) ‚ãÖ (z‚ÇÇ·µ¢ - z‚ÇÇ‚±º)

      # bin (or lag) where to accumulate result
      lag = ceil(Int, h / Œ¥h)
      lag == 0 && @warn "duplicate coordinates found"

      if 0 < lag ‚â§ nlags && !ismissing(v)
        xsums[lag] += h
        ysums[lag] += v
        counts[lag] += 1
      end
    end
  end

  xsums, ysums, counts
end

# temporary fix for ‚ãÖ with missing values
# https://github.com/JuliaLang/julia/issues/40743
‚ãÖ(::Missing, ::Missing) = missing
