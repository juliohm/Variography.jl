# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

# default maximum lag to be used in various methods
default_maxlag(data) = 0.1diagonal(boundingbox(data))

"""
    EmpiricalVariogram(data, varâ‚, varâ‚‚=varâ‚; [optional parameters])

Computes the empirical (a.k.a. experimental) omnidirectional
(cross-)variogram for variables `varâ‚` and `varâ‚‚` stored in
spatial `data`.

## Parameters

  * nlags    - number of lags (default to `20`)
  * maxlag   - maximum lag (default to half of maximum lag of data)
  * distance - custom distance function (default to `Euclidean` distance)
  * algo     - accumulation algorithm (default to `:ball`)

Available algorithms:

  * `:full` - loop over all pairs of points in the data
  * `:ball` - loop over all points inside maximum lag ball

All implemented algorithms produce the exact same result.
The `:ball` algorithm is considerably faster when the
maximum lag is much smaller than the bounding box of
the data.

The function [`values`](@ref) can be used to retrieve
the abscissa, ordinate and bin count of an empirical
variogram:

```julia
julia> x, y, n = values(Î³)
```

See also: [`DirectionalVariogram`](@ref)

## References

* ChilÃ¨s, JP and Delfiner, P. 2012. [Geostatistics: Modeling Spatial Uncertainty]
  (https://onlinelibrary.wiley.com/doi/book/10.1002/9781118136188)
* Hoffimann, J and Zadrozny, B. 2019. [Efficient variography with partition variograms]
  (https://www.sciencedirect.com/science/article/pii/S0098300419302936)
"""
struct EmpiricalVariogram{D}
  abscissa::Vector{Float64}
  ordinate::Vector{Float64}
  counts::Vector{Int}
  distance::D
end

EmpiricalVariogram(abscissa, ordinate, counts, distance) =
  EmpiricalVariogram{typeof(distance)}(abscissa, ordinate, counts, distance)

function EmpiricalVariogram(data, varâ‚::Symbol, varâ‚‚::Symbol=varâ‚;
                            nlags=20, maxlag=default_maxlag(data),
                            distance=Euclidean(), algo=:ball)
  # sanity checks
  @assert nelements(data) > 1 "variogram requires at least 2 elements"
  @assert (varâ‚, varâ‚‚) âŠ† name.(variables(data)) "invalid variable names"
  @assert algo âˆˆ (:full, :ball) "invalid accumulation algorithm"
  @assert nlags  > 0 "number of lags must be positive"
  @assert maxlag > 0 "maximum lag distance must be positive"

  # retrieve table and domain
  ğ’¯ = values(data)
  ğ’Ÿ = domain(data)

  # ball search with NearestNeighbors.jl requires AbstractFloat and MinkowskiMetric
  # https://github.com/KristofferC/NearestNeighbors.jl/issues/13
  isfloat     = coordtype(data) <: AbstractFloat
  isminkowski = distance isa MinkowskiMetric

  # warn users requesting :ball option with invalid parameters
  (algo == :ball && !isfloat) && @warn ":ball algorithm requires floating point coordinates"
  (algo == :ball && !isminkowski) && @warn ":ball algorithm requires Minkowski metric"

  # empirical variograms are defined on point sets
  ğ’« = PointSet([centroid(ğ’Ÿ, i) for i in 1:nelements(ğ’Ÿ)])
  ğ’® = georef(ğ’¯, ğ’«)

  # choose accumulation algorithm
  if algo == :ball && isfloat && isminkowski
    xsums, ysums, counts = ball_search_accum(ğ’®, varâ‚, varâ‚‚, maxlag, nlags, distance)
  else
    xsums, ysums, counts = full_search_accum(ğ’®, varâ‚, varâ‚‚, maxlag, nlags, distance)
  end

  # bin (or lag) size
  Î´h = maxlag / nlags
  lags = range(Î´h/2, stop=maxlag - Î´h/2, length=nlags)

  # variogram abscissa
  abscissa = @. xsums / counts
  abscissa[counts .== 0] .= lags[counts .== 0]

  # variogram ordinate
  ordinate = @. (ysums / counts) / 2
  ordinate[counts .== 0] .= 0

  EmpiricalVariogram(abscissa, ordinate, counts, distance)
end

"""
    values(Î³)

Returns the abscissa, the ordinate, and the bin counts
of the empirical variogram `Î³`.
"""
Base.values(Î³::EmpiricalVariogram) = Î³.abscissa, Î³.ordinate, Î³.counts

"""
    distance(Î³)

Retun the distance used to compute the empirical variogram `Î³`.
"""
distance(Î³::EmpiricalVariogram) = Î³.distance

"""
    merge(Î³Î±, Î³Î²)

Merge the empirical variogram `Î³Î±` with the empirical variogram `Î³Î²`
assuming that both variograms have the same number of lags.
"""
function merge(Î³Î±::EmpiricalVariogram{D}, Î³Î²::EmpiricalVariogram{D}) where {D}
  xÎ± = Î³Î±.abscissa
  xÎ² = Î³Î².abscissa
  yÎ± = Î³Î±.ordinate
  yÎ² = Î³Î².ordinate
  nÎ± = Î³Î±.counts
  nÎ² = Î³Î².counts

  n = nÎ± + nÎ²
  x = @. (xÎ±*nÎ± + xÎ²*nÎ²) / n
  y = @. (yÎ±*nÎ± + yÎ²*nÎ²) / n

  # adjust empty bins
  x[n .== 0] .= xÎ±[n .== 0]
  y[n .== 0] .= 0

  d = Î³Î±.distance

  EmpiricalVariogram(x, y, n, d)
end

# -----------
# IO METHODS
# -----------

function Base.show(io::IO, ::EmpiricalVariogram)
  print(io, "EmpiricalVariogram")
end

function Base.show(io::IO, ::MIME"text/plain", Î³::EmpiricalVariogram)
  println(io, Î³)
  println(io, "  abscissa: ", extrema(Î³.abscissa))
  println(io, "  ordinate: ", extrema(Î³.ordinate))
  print(  io, "  NÂ° pairs: ", sum(Î³.counts))
end

# ------------------------
# ACCUMULATION ALGORITHMS
# ------------------------

function full_search_accum(data, varâ‚, varâ‚‚, maxlag, nlags, distance)
  # retrieve table and point set
  ğ’¯ = values(data)
  ğ’« = domain(data)

  # lag size
  Î´h = maxlag / nlags

  # lag sums and counts
  xsums = zeros(nlags)
  ysums = zeros(nlags)
  counts = zeros(Int, nlags)

  # collect vectors for variables
  cols = Tables.columns(ğ’¯)
  Zâ‚   = Tables.getcolumn(cols, varâ‚)
  Zâ‚‚   = Tables.getcolumn(cols, varâ‚‚)

  # loop over all pairs of points
  @inbounds for j in 1:nelements(ğ’«)
    pâ±¼  = ğ’«[j]
    zâ‚â±¼ = Zâ‚[j]
    zâ‚‚â±¼ = Zâ‚‚[j]
    for i in j+1:nelements(ğ’«)
      páµ¢  = ğ’«[i]
      zâ‚áµ¢ = Zâ‚[i]
      zâ‚‚áµ¢ = Zâ‚‚[i]

      # evaluate spatial lag
      h = evaluate(distance, coordinates(páµ¢), coordinates(pâ±¼))
      h > maxlag && continue # early exit if out of range

      # evaluate (cross-)variance
      v = (zâ‚áµ¢ - zâ‚â±¼) * (zâ‚‚áµ¢ - zâ‚‚â±¼)

      # bin (or lag) where to accumulate result
      lag = ceil(Int, h / Î´h)
      lag == 0 && @warn "duplicate coordinates found"

      if 0 < lag â‰¤ nlags && !ismissing(v)
        xsums[lag] += h
        ysums[lag] += v
        counts[lag] += 1
      end
    end
  end

  xsums, ysums, counts
end

function ball_search_accum(data, varâ‚, varâ‚‚, maxlag, nlags, distance)
  # retrieve table and point set
  ğ’¯ = values(data)
  ğ’« = domain(data)

  # lag size
  Î´h = maxlag / nlags

  # lag sums and counts
  xsums = zeros(nlags)
  ysums = zeros(nlags)
  counts = zeros(Int, nlags)

  # collect vectors for variables
  cols = Tables.columns(ğ’¯)
  Zâ‚   = Tables.getcolumn(cols, varâ‚)
  Zâ‚‚   = Tables.getcolumn(cols, varâ‚‚)

  # fast ball search
  ball = MetricBall(maxlag, distance)
  searcher = BallSearch(ğ’«, ball)

  # loop over points inside ball
  @inbounds for j in 1:nelements(ğ’«)
    pâ±¼  = ğ’«[j]
    zâ‚â±¼ = Zâ‚[j]
    zâ‚‚â±¼ = Zâ‚‚[j]
    for i in search(pâ±¼, searcher)
      i â‰¤ j && continue # avoid double counting
      páµ¢  = ğ’«[i]
      zâ‚áµ¢ = Zâ‚[i]
      zâ‚‚áµ¢ = Zâ‚‚[i]

      # evaluate spatial lag
      h = evaluate(distance, coordinates(páµ¢), coordinates(pâ±¼))

      # evaluate (cross-)variance
      v = (zâ‚áµ¢ - zâ‚â±¼) * (zâ‚‚áµ¢ - zâ‚‚â±¼)

      # bin (or lag) where to accumulate result
      lag = ceil(Int, h / Î´h)
      lag == 0 && @warn "duplicate coordinates found"

      if 0 < lag â‰¤ nlags && !ismissing(v)
        xsums[lag] += h
        ysums[lag] += v
        counts[lag] += 1
      end
    end
  end

  xsums, ysums, counts
end
