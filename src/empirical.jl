# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    VariogramEstimator

A (robust) estimator of [`EmpiricalVariogram`](@ref).
"""
abstract type VariogramEstimator end

include("estimators/matheron.jl")

"""
    VariogramAccumAlgo

Algorithm for accumulating pairs of points in
[`EmpiricalVariogram`](@ref) estimation.
"""
abstract type VariogramAccumAlgo end

"""
    accumulate(data, varâ‚, varâ‚‚, algo)

Accumulate pairs of points in `data` for variables
`varâ‚` and `varâ‚‚` with accumulation algorithm `algo`.
"""
function accumulate(data, varâ‚, varâ‚‚, algo::VariogramAccumAlgo)
  # retrieve algorithm parameters
  maxlag = algo.maxlag
  nlags = algo.nlags
  distance = algo.distance
  estimator = algo.estimator

  # retrieve table and point set
  ğ’¯ = values(data)
  ğ’« = domain(data)

  # retrieve neighbors function
  neighbors = neighfun(ğ’«, algo)

  # retrieve skip condition
  skip = skipfun(algo)

  # retrieve early exit condition
  exit = exitfun(algo)

  # collect vectors for variables
  cols = Tables.columns(ğ’¯)
  Zâ‚ = Tables.getcolumn(cols, varâ‚)
  Zâ‚‚ = Tables.getcolumn(cols, varâ‚‚)

  # lag size
  Î´h = maxlag / nlags

  # accumulation type
  V = typeof((Zâ‚[1] - Zâ‚‚[1]) â‹… (Zâ‚[1] - Zâ‚‚[1]))

  # lag sums and counts
  xsums = zeros(nlags)
  ysums = zeros(V, nlags)
  counts = zeros(Int, nlags)

  # loop over points inside ball
  @inbounds for j in 1:nelements(ğ’«)
    pâ±¼ = ğ’«[j]
    zâ‚â±¼ = Zâ‚[j]
    zâ‚‚â±¼ = Zâ‚‚[j]
    for i in neighbors(j)
      # skip to avoid double counting
      skip(i, j) && continue

      páµ¢ = ğ’«[i]
      zâ‚áµ¢ = Zâ‚[i]
      zâ‚‚áµ¢ = Zâ‚‚[i]

      # evaluate geospatial lag
      h = evaluate(distance, coordinates(páµ¢), coordinates(pâ±¼))

      # early exit if out of range
      exit(h) && continue

      # evaluate (cross-)variance
      v = (zâ‚áµ¢ - zâ‚â±¼) â‹… (zâ‚‚áµ¢ - zâ‚‚â±¼)

      # bin (or lag) where to accumulate result
      lag = ceil(Int, h / Î´h)
      lag == 0 && @warn "duplicate coordinates found, consider using `UniqueCoords`"

      if 0 < lag â‰¤ nlags && !ismissing(v)
        xsums[lag] += h
        ysums[lag] += v
        counts[lag] += 1
      end
    end
  end

  xsums, ysums, counts
end

include("algorithms/fullsearch.jl")
include("algorithms/ballsearch.jl")

# ---------------------
# EMPIRICAL VARIOGRAMS
# ---------------------

include("empirical/variogram.jl")
include("empirical/varioplane.jl")
include("empirical/partition.jl")
